---
layout: post
title: "Testing x-ocaml, OCaml notebooks as a WebComponent"
date: 2025-06-20 10:00
categories: [OCaml, X-OCaml, Blogging]
excerpt_separator: <!--more-->
---

<script async
  src="{{ '/assets/x-ocaml.js' | absolute_url }}"
  src-worker="{{ '/assets/x-ocaml.worker+effects.js' | absolute_url }}"
  src-load="{{ '/assets/digestif.js' | absolute_url }}"
></script>

Can we have OCaml notebooks as pure client-side code? Can these notebooks have
rich editor support (types on hover, autocompletion, inline diagnostics,
semantic highlighting, etc.)? Can you take packages from OPAM and use them in
these notebooks? 

The answer to all of these turns out to be a resounding yes thanks for
[x-ocaml](https://github.com/art-w/x-ocaml). This post is my experiment playing
with `x-ocaml`. 

<!--more-->

The most wonderful thing about programming is that it lets you experiment
freely. You can try out an idea, get instant feedback, and learn by doingâ€”much
like playing with Lego bricks or sketching on a canvas. The two main courses
that I teach at IITM, [CS3100](https://github.com/kayceesrk/cs3100_m20) and
[CS6225](https://github.com/kayceesrk/cs6225_s25_iitm/), both involve me
live-coding during every lecture. However, blogging about OCaml where the code
is static and non-interactive always felt a bit unsatisfying. 

Enter [x-ocaml](https://github.com/art-w/x-ocaml), which allows for a way to
embed OCaml notebooks into any webpage thanks to WebComponents. All you need to
do is to load some JavaScript in your webpage and you can start embedding code
cells using `<x-ocaml>` tag. The snippet below:

```
<x-ocaml>
print_endline "Hello, world"
</x-ocaml>
```

renders to:

<x-ocaml>
print_endline "Hello, world"
</x-ocaml>

The code is interpreted in the browser thanks to the OCaml interpreter compiled
to JavaScript through the
[Js_of_ocaml](https://ocsigen.org/js_of_ocaml/latest/manual/overview) compiler.
There is also support for [Merlin](https://github.com/ocaml/merlin) and
[OCamlformat](https://github.com/ocaml-ppx/ocamlformat) in the code editor. Try
hovering over the functions and writing some code. You should see inferred types
and auto-completion suggestions. It turns out that this solution integrates well
with Jekyll, which is what I use for this blog.

## Reverse-mode AD using Effects

Js_of_ocaml also supports [effect
handlers](https://ocsigen.org/js_of_ocaml/latest/manual/effects). Here's the
implementation of [reverse-mode algorithmic
differentiation](https://github.com/ocaml-multicore/effects-examples/blob/master/algorithmic_differentiation.ml),
implemented using effect handlers running in the browser.

<x-ocaml>
open Effect
open Effect.Deep

module F : sig
  type t

  val mk : float -> t
  val ( +. ) : t -> t -> t
  val ( *. ) : t -> t -> t
  val grad : (t -> t) -> float -> float
  val grad2 : (t * t -> t) -> float * float -> float * float
end = struct
  type t = { v : float; mutable d : float }

  let mk v = { v; d = 0.0 }

  type _ eff += Add : t * t -> t eff
  type _ eff += Mult : t * t -> t eff

  let run f =
    ignore (match f () with
      | r -> r.d <- 1.0; r;
      | effect (Add(a,b)), k ->
          let x = {v = a.v +. b.v; d = 0.0} in
          ignore (continue k x);
          a.d <- a.d +. x.d;
          b.d <- b.d +. x.d;
          x
      | effect (Mult(a,b)), k ->
          let x = {v = a.v *. b.v; d = 0.0} in
          ignore (continue k x);
          a.d <- a.d +. (b.v *. x.d);
          b.d <- b.d +. (a.v *. x.d);
          x)

  let grad f x =
    let x = mk x in
    run (fun () -> f x);
    x.d

  let grad2 f (x, y) =
    let x, y = (mk x, mk y) in
    run (fun () -> f (x, y));
    (x.d, y.d)

  let ( +. ) a b = perform (Add (a, b))
  let ( *. ) a b = perform (Mult (a, b))
end
</x-ocaml>

Here are some tests.

<x-ocaml>

(* XXX KC: `assert` from standard library doesn't work. Why? *)
let assert' c = 
  if not c then raise (Failure "assertion failed!")

let test1 =
  (* f = x + x^3 =>
     df/dx = 1 + 3 * x^2 *)
  for x = 0 to 10 do
    let x = float_of_int x in
    let d1 = F.(grad (fun x -> x +. (x *. x *. x)) x) in
    let d2 = 1.0 +. (3.0 *. x *. x) in
    Printf.printf "%f %f\n" d1 d2;
    assert' ( d1 = d2 )
  done

let test2 = 
  (* f = x^2 + x^3 =>
     df/dx = 2*x + 3 * x^2 *)
  for x = 0 to 10 do
    let x = float_of_int x in
    assert' (
      F.(grad (fun x -> (x *. x) +. (x *. x *. x)) x)
      = (2.0 *. x) +. (3.0 *. x *. x))
  done

let test3 =
  (* f = x^2 * y^4 =>
     df/dx = 2 * x * y^4
     df/dy = 4 * x^2 * y^3 *)
  for x = 0 to 10 do
    for y = 0 to 10 do
      let x = float_of_int x in
      let y = float_of_int y in
      assert' (
        F.(grad2 (fun (x, y) -> x *. x *. y *. y *. y *. y) (x, y))
        = (2.0 *. x *. y *. y *. y *. y, 4.0 *. x *. x *. y *. y *. y))
    done
  done
</x-ocaml>

## Using other libraries

`x-ocaml` also supports loading any js_of_ocaml compatible library into the
webpage. Let's use [`digestif`](https://github.com/mirage/digestif).

For any library that you want to export, install the library using opam.
`x-ocaml` provide a command-line utility to export the library.

```bash
$ x-ocaml --effects digestif.ocaml -o digestif.js
```

This produces the JavaScript artifact that can be used in the webpage. It may be
instructive to look at the
[source](https://github.com/kayceesrk/kayceesrk.github.io/blame/54ef5eea28c660aa0d8b3cd2e32d8e93d713ab19/_posts/2025-06-20-xocaml.md)
of this post to see how the compiler and the libraries are integrated into this
blog post. There is a little script at the top of the file:

```javascript
{% raw %}
<script async
  src="{{ '/assets/x-ocaml.js' | absolute_url }}"
  src-worker="{{ '/assets/x-ocaml.worker+effects.js' | absolute_url }}"
  src-load="{{ '/assets/digestif.js' | absolute_url }}"
></script>
{% endraw %}
```

<x-ocaml>
let hash = Digestif.MD5.(digest_string "hello" |> to_hex)
</x-ocaml>

## What next?

There is a number of rough edges to `x-ocaml`. This is expected since this
project appears to be one of [Arthur's](https://github.com/art-w) hacking
expeditions (which, as usual, is pushing the state of the art forward). It would
be fun to use this for teaching
[CS3100](https://github.com/kayceesrk/cs3100_m20) and also
[other](https://github.com/kayceesrk/learn-ocaml-workshop-2024)
[OCaml](https://github.com/ocaml-multicore/ocaml-effects-tutorial)
[tutorials](https://github.com/ocaml-multicore/parallel-programming-in-multicore-ocaml).
Perhaps even have an interactive version of [Real World OCaml
book](https://dev.realworldocaml.org/).
